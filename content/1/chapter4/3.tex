有了前面部分的知识，是时候编写我们自己的甜甜圈了!我们将按照以下步骤进行:

\begin{enumerate}
\item 要创建的第一个文件是\texttt{Kitchen.td}。定义了烹饪环境，包括测量单位、设备和程序，仅举几个方面。我们将从测量单位开始:

\begin{lstlisting}[style=styleCXX]
class Unit {
	string Text;
	bit Imperial;
}
\end{lstlisting}

这里，Text字段是菜谱上显示的文本格式，Imperial只是一个布尔标志，用于标记这个单位是英制还是公制单位。每个重量或体积单元将是继承自这个类的一个记录——下面的代码是一个例子:

\begin{lstlisting}[style=styleCXX]
def gram_unit : Unit {
	let Imperial = false;
	let Text = "g";
}
def tbsp_unit : Unit {
	let Imperial = true;
	let Text = "tbsp";
}
\end{lstlisting}

我们需要创建许多度量单位，但是代码已经相当长了。简化和使它更可读的方法是使用类模板参数，如下所示:

\begin{lstlisting}[style=styleCXX]
class Unit<bit imperial, string text> {
	string Text = text;
	bit Imperial = imperial;
}
def gram_unit : Unit<false, "g">;
def tbsp_unit : Unit<true, "tbsp">;
\end{lstlisting}

与\texttt{C++}的模板参数不同，TableGen中的模板参数只接受具体的值，只是为字段赋值的另一种方法。

\item 由于TableGen不支持浮点数，需要定义一些方法来表示数字，例如\textbf{1和$\frac{1}{4}$杯}或\textbf{94.87g的面粉}。一种解决办法是使用定点数，如下所示:

\begin{lstlisting}[style=styleCXX]
class FixedPoint<int integral, int decimal = 0> {
	int Integral = integral;
	int DecimalPoint = decimal;
}
def one_plus_one_quarter : FixedPoint<125, 2>; // Shown
as 1.25
\end{lstlisting}

提到\texttt{Integral}和\texttt{DecimalPoint}字段，\texttt{FixedPoint}类表示的值等于下面的公式:

\textit{Integral * 10\^(-DecimalPoint)}

$\frac{1}{4}$、$\frac{1}{2}$和$\frac{3}{4}$显然是常用的测量单位(特别是英制单位，如美制杯)，使用一个helper类来创建它们可能是个好主意:

\begin{lstlisting}[style=styleCXX]
class NplusQuarter<int n, bits<2> num_quarter> :
FixedPoint<?, 2> {…}
def one_plus_one_quarter : NplusQuarter<1,1>; // Shown as
1.25
\end{lstlisting}

这将使表示数量，如N和$\frac{1}{4}$ 杯或N和$\frac{1}{2}$杯变得更加容易。

TableGen类也有继承——一个类可以继承一个或多个类。由于TableGen没有成员函数/方法的概念，继承类只是简单地集成字段。

\item 为了实现\texttt{NplusQuarter}，特别是从\texttt{NplusQuarter}类模板参数到\texttt{FixedPoint}的转换，我们需要一些简单的算术计算，这就是使用TableGen的叹号操作符的位置:

\begin{lstlisting}[style=styleCXX]
class NplusQuarter<int n, bits<2> num_quarter> :
FixedPoint<?, 2> {
	int Part1 = !mul(n, 100);
	int Part2 = !mul(25, !cast<int>(num_quarter{1...0}));
	let Integral = !add(Part1, Part2);
}
\end{lstlisting}

另一个有趣的语法是\texttt{num\_quarter}的位提取(或切片)。通过\texttt{num\_quarter\{1…0\}}，这返回一个\texttt{bits}值，这个值的第0位和第1位与\texttt{num\_quarter}第0位和第1位一样。这种技术还有其他一些变体。例如，可以对非连续范围的位进行切片:

\begin{lstlisting}[style=styleCXX]
num_quarter{8…6,4,2…0}
\end{lstlisting}

或者，它可以反向提取位，如下所示:

\begin{lstlisting}[style=styleCXX]
num_quarter{1…7}
\end{lstlisting}

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black, fonttitle=\bfseries,title=Note]
\hspace*{0.7cm}即使它已经声明num\_quarter的宽度为2位(bits<2>类型)，读者们可能也想知道为什么代码需要显式地提取最小的2位。由于某些原因，TableGen不会阻止将大于3的值赋给\texttt{num\_quarter}，例如\texttt{:def x: NplusQuarter<1,1999>}。
\end{tcolorbox}

\item 有了计量单位和数字格式，就可以处理这个配方所需的配料了。首先，让我们使用一个文件\texttt{Ingredients.td}储存所有配料信息。为了使用前面提到的所有东西，可以使用包含语法导入\texttt{Kitchen.td}:

\begin{lstlisting}[style=styleCXX]
// In Ingredients.td…
include "Kitchen.td"
\end{lstlisting}

然后，创建所有成分的基类(有一些公共字段):

\begin{lstlisting}[style=styleCXX]
class IngredientBase<Unit unit> {
	Unit TheUnit = unit;
	FixedPoint Quantity = FixedPoint<0>;
}
\end{lstlisting}

每种食材都由\texttt{IngredientBase}派生的类表示，这些类都有参数来指定配方所需的量，以及计量食材的单位。以牛奶为例，如下所示:

\begin{lstlisting}[style=styleCXX]
class Milk<int integral, int num_quarter> :
IngredientBase<cup_unit> {
	let Quantity = NplusQuarter<integral, num_quarter>;
}
\end{lstlisting}

\texttt{IngredientBased}的模板参数\texttt{cup\_unit}表示，牛奶用美制杯为单位，数量将由Milk类模板参数确定。

编写一个配方时，每个成分都由一个记录表示，该记录由这些成分类创建:

\begin{lstlisting}[style=styleCXX]
def ingredient_milk : Milk<1,2>; // Need 1.5 cup of milk
\end{lstlisting}

\item 然而，有些食材总是混在一起——例如，柠檬皮和柠檬汁，蛋黄和蛋清。也就是说，如果你有两个蛋黄，就必须有两份蛋清。但是，如果需要创建一个记录，并为每个成分分配数，这样就会出现很多重复的代码。为了优雅的解决这个问题，这里使用TableGen的多类语法。

以鸡蛋为例，假设想要同时创建相同数量的\texttt{WholeEgg}、\texttt{EggWhite}和\texttt{egggyolk}记录，首先需要定义\texttt{multiclass}:

\begin{lstlisting}[style=styleCXX]
multiclass Egg<int num> {
	def _whole : WholeEgg {
		let Quantity = FixedPoint<num>;
	}
	def _yolk : EggYolk {
		let Quantity = FixedPoint<num>;
	}
	def _white : EggWhite {
		let Quantity = FixedPoint<num>;
	}
}
\end{lstlisting}

编写配方时，使用\texttt{defm}语法创建\texttt{multiclass}记录:

\begin{lstlisting}[style=styleCXX]
defm egg_ingredient : Egg<3>;
\end{lstlisting}

使用\texttt{defm}之后，将创建三个记录：\texttt{egg\_ingredient\_whole}、\texttt{egg\_ingredient\_yolk}和\\\texttt{egg\_ingredient\_white}，分别继承于\texttt{WholEgg}、\texttt{EggYolk}和\texttt{EggWhite}。

\item 最后，我们需要一种方法来描述制作甜甜圈的步骤。许多食谱都有一些准备步骤，不需要按照特定的顺序来完成。以甜甜圈食谱为例：在油炸甜甜圈之前，随时都可以预热油。因此，用\texttt{dag}类型表示烘焙步骤可能是个好主意。

先创建一个类来表示烘焙步骤:

\begin{lstlisting}[style=styleCXX]
class Step<dag action, Duration duration, string custom_
format> {
	dag Action = action;
	Duration TheDuration = duration;
	string CustomFormat = custom_format;
	string Note;
}
\end{lstlisting}

\texttt{Action}字段包含烘焙说明和有关所用配料的信息:

\begin{lstlisting}[style=styleCXX]
def mix : Action<"mix",…>;
def milk : Milk<…>;
def flour : Flour<…>;
def step_mixing : Step<(mix milk, flour), …>;
\end{lstlisting}

\texttt{Action}只是用来描述动作的类。下面的代码代表了\texttt{step\_mixing2}使用\texttt{step\_mixing2}的结果(可能是生面团)，并将其与黄油混合的过程:

\begin{lstlisting}[style=styleCXX]
…
def step_mixing : Step<(mix milk, flour), …>;
def step_mixing2 : Step<(mix step_mixing, butter), …>;
\end{lstlisting}

最终，所有的\texttt{Step}记录将形成一个DAG，其中的顶点是一个\texttt{Step}的记录，或是一个成分的记录。

这里，还用标签来注释\texttt{dag}操作符和操作数，如下所示:

\begin{lstlisting}[style=styleCXX]
def step_mixing2 : Step<(mix:$action step_mixing:$dough, butter)>
\end{lstlisting}

前一节TableGen语法介绍中，说过这些\texttt{dag}标签在TableGen代码中，除了影响TableGen后端处理当前记录的方式，并没有即时效果——例如，在\texttt{Step}类中有一个\texttt{string}类型字段\texttt{CustomFormat}，如下所示:

\begin{lstlisting}[style=styleCXX]
def step_prep : Step<(heat:$action fry_oil:$oil, oil_
temp:$temp)> {
	let CustomFormat = "$action the $oil until $temp";
}
\end{lstlisting}

显示字段内容后，可以用记录的文本表示替换字符串中的\texttt{\$action}、\texttt{\$oil}和\texttt{\$temp}，生成一个字符串，例如:\texttt{花生油加热至300华氏度}。

\end{enumerate}

下一节的目标是开发一个自定义TableGen后端，将这里的TableGen版本的菜谱作为输入，并打印出纯文本的菜谱。




















